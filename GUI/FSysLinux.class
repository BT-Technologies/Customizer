' Gambas class file

PUBLIC SUB Form_Open()
  DIM HFColor, HBColor, HSColor, SplashFile AS Variant
  DIM WorkDir AS String
  
  ME.Center
  ME.Caption = "SysLinux"
  
  Check.Conf_File()
  
  WorkDir = Functions.Get_Conf("WORK_DIR=")
  
  IF NOT Exist(WorkDir & "/ISO/isolinux/splash.pcx") AND NOT Exist(WorkDir & "/ISO/isolinux/splash.jpg") THEN
    Message.Warning("Splash file doesn't exists.")
    ChangePic.Enabled = FALSE
  ELSE 
    IF Exist(WorkDir & "/ISO/isolinux/splash.pcx") THEN
      SplashFile = "splash.pcx"
    ELSE 
      SplashFile = "splash.jpg"
    ENDIF 
    
    SplashPic.Picture = Picture.Load(WorkDir & "/ISO/isolinux/" & SplashFile)
  ENDIF 

  IF NOT Exist(WorkDir & "/ISO/isolinux/gfxboot.cfg") THEN 
    Message.Warning("gfxboot.cfg doesn't exists")
    SetColourMode.Enabled = FALSE
    ColorChooser1.Enabled = FALSE
  ELSE 
    HFColor = Functions.Get_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "foreground=0x")
    HBColor = Functions.Get_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "background=0x")
    HSColor = Functions.Get_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "screen-colour=0x")
    
    Selected.ForeColor = Val("&H" & HFColor) 
    Selected.BackColor = Val("&H" & HSColor)
    Normal.ForeColor = Val("&H" & HBColor) 
    Panel1.BackColor = Val("&H" & HSColor)
    ColorChooser1.ToolTip = "Change the colour used for " & SetColourMode.Text
  ENDIF 
  
  IF NOT Exist(WorkDir & "/ISO/isolinux/splash.pcx") AND NOT Exist(WorkDir & "/ISO/isolinux/splash.jpg") AND NOT Exist(WorkDir & "/ISO/isolinux/gfxboot.cfg") THEN
    Message.Error("Nothing to do here")
    ME.Close
  ENDIF 
END

PUBLIC SUB Quit_Click()
  ME.Close
END


PUBLIC SUB ChangePic_Click()
  DIM PIC, WorkDir, SplashFile AS String
  
  WorkDir = Functions.Get_Conf("WORK_DIR=")
  PIC = Functions.Get_Conf("PIC=")
  
  Dialog.Title = "Please select picture"
  Dialog.Filter = ["*.pcx;*.png;*.bmp;*.jpeg;*.jpg", "Pictures"]
  
  IF Exist(PIC) THEN
    Dialog.Path = PIC
  ELSE 
    Dialog.Path = "/home"
  ENDIF  
  
  IF Exist(WorkDir & "/ISO/isolinux/splash.pcx") THEN
      SplashFile = "splash.pcx"
  ELSE 
      SplashFile = "splash.jpg"
  ENDIF 
  
  IF Dialog.OpenFile() THEN RETURN 
  Functions.Replace_Conf("PIC=", Dialog.Path)
  SHELL "convert -resize 640x480! -colors 256 '" & Dialog.Path & "' '" & WorkDir & "/ISO/isolinux/" & SplashFile & "'" WAIT
  SplashPic.Picture = Picture.Load(WorkDir & "/ISO/isolinux/" & SplashFile)
END

PUBLIC SUB ColorChooser1_Change()
  DIM WorkDir AS Variant
  
  WorkDir = Functions.Get_Conf("WORK_DIR=")

  SELECT CASE SetColourMode.Text
    CASE "Selected"
    Selected.ForeColor = ColorChooser1.Value
    Functions.Replace_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "foreground=0x", Hex(ColorChooser1.Value, 6))
  CASE "Normal"
    Normal.ForeColor = ColorChooser1.Value
    Functions.Replace_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "background=0x", Hex(ColorChooser1.Value, 6))
  CASE "Screen-Colour"
    Panel1.BackColor = ColorChooser1.Value
    Selected.BackColor = ColorChooser1.Value
    Functions.Replace_Str(WorkDir & "/ISO/isolinux/gfxboot.cfg", "screen-colour=0x", Hex(ColorChooser1.Value, 6))
  END SELECT
END

PUBLIC SUB SetColourMode_Click()
  ColorChooser1.ToolTip = "Change the colour used for " & SetColourMode.Text
END
